apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://maven.fabric.io/public' }
  }
  dependencies {
    classpath pluginDependencies.gradle
    classpath pluginDependencies.kotlin
    classpath pluginDependencies.coveralls
    classpath pluginDependencies.errorprone
    classpath pluginDependencies.dependencyUpdates
    classpath pluginDependencies.jacoco
    classpath pluginDependencies.playPublisher
    classpath pluginDependencies.googleServices
    classpath pluginDependencies.crashlytics
  }
}

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://mapbox.bintray.com/mapbox' }
    // Snapshot repository
     maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
  }

  group = GROUP
  version = VERSION_NAME
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'
  apply from: "${rootDir}/gradle/dependencies.gradle"

  dependencies {
    errorprone dependenciesList.errorprone
  }

  project.configurations.all {
    resolutionStrategy.eachDependency { details ->
      if (details.requested.group == "com.mapbox.mapboxsdk" && details.requested.name
              == 'mapbox-android-telemetry' ) {
        details.useVersion "4.5.0-SNAPSHOT"
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}